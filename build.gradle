buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            url = "https://jitpack.io"
        }
    }
    dependencies {
        classpath 'com.github.GTNewHorizons:ForgeGradle:1.2.11'
    }
}

apply plugin: 'forge'

version = "1.0"
group= "io.github.legacymoddingmc.example" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "UniMixinsExample"
def modid = "unimixinsexample"

minecraft {
    version = "1.7.10-10.13.4.1614-1.7.10"
    runDir = "run"
}

repositories {
    maven {
        // Baubles
		name = "Overmind forge repo mirror"
		url = "https://gregtech.overminddl1.com/"
	}
}

dependencies {
    // you may put jars on which you depend on in ./libs
    // or you may define them like so..
    //compile "some.group:artifact:version:classifier"
    //compile "some.group:artifact:version"
      
    // real examples
    //compile 'com.mod-buildcraft:buildcraft:6.0.8:dev'  // adds buildcraft to the dev env
    //compile 'com.googlecode.efficient-java-matrix-library:ejml:0.24' // adds ejml to the dev env

    // for more info...
    // http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
    // http://www.gradle.org/docs/current/userguide/dependency_management.html
    
    // Baubles is our test subject for late mixins
    compile "com.azanor.baubles:Baubles:1.7.10-1.0.1.10:deobf"
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

// -- Begin UniMixins section --

// -- Add UniMixins dependency --

repositories {
    maven {
        url 'https://jitpack.io'
    }
}

def unimixinsVersion = "0.1.14"

dependencies {
    compile("com.github.LegacyModdingMC.UniMixins:unimixins-all-1.7.10:$unimixinsVersion:dev")
}

// Exclude conflicting transitive dependencies.
// Only needed if you depend on a mod that depends on one of these.
// This may need to be at the end of the build script.

configurations.compile.dependencies.each {
    if (it instanceof ExternalModuleDependency) {
        it.exclude module: "SpongeMixins"
        it.exclude module: "SpongePoweredMixin"
        it.exclude module: "00gasstation-mc1.7.10"
        it.exclude module: "gtnhmixins"
    }
}

// -- Generic Mixin setup --

runClient {
    args("--tweakClass", "org.spongepowered.asm.launch.MixinTweaker")
}

runServer {
    args("--tweakClass", "org.spongepowered.asm.launch.MixinTweaker")
}

def outSrgFile = "${tasks.compileJava.temporaryDir}/outSrg.srg"
def outRefMapFile = "${tasks.compileJava.temporaryDir}/mixins.${modid}.refmap.json"

jar {
    manifest {
        attributes (
            // This will bootstrap Mixin and make your mod discoverable by it.
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            
            // This is the normal way of registering Mixin configs.
            // Mixin will automatically add any that are specified here.
            'MixinConfigs': "mixins.${modid}.json",
            
            // To use the IEarlyMixinLoader interface, an FML plugin needs to be
            // registered.
            'FMLCorePlugin': 'io.github.legacymoddingmc.unimixins.example.ExampleCore',
            
            // If your mod also contains a Forge mod (marked by a @Mod annotation),
            // these need to be set or it will get ignored.
            // Note that a Forge mod is required for late mixins to work.
            'ForceLoadAsMod': 'true',
            'FMLCorePluginContainsFMLMod': 'true',
        )
    }
    
    from outRefMapFile;
}

tasks.compileJava.options.compilerArgs += ["-AreobfSrgFile=${tasks.reobf.srg}", "-AoutSrgFile=${outSrgFile}", "-AoutRefMapFile=${outRefMapFile}"];

reobf {
    addExtraSrgFile outSrgFile
}
